buildscript {
    repositories {
	jcenter()
    }
    dependencies {
	classpath 'com.android.tools.build:gradle:2.3.0'
    }
}
apply plugin: 'com.android.application'

android {
    compileSdkVersion 'android-26'
    buildToolsVersion '26.0.1'

    defaultConfig {
	minSdkVersion 7
	targetSdkVersion 26
    }

    signingConfigs {
	release {
	    storeFile file("../release.keystore")
	    keyAlias "android-release-key"
	}
    }

    buildTypes {
	release {
	    minifyEnabled false
	    signingConfig signingConfigs.release
	    proguardFile getDefaultProguardFile('proguard-android.txt')
	}
    }
}

dependencies {
}

repositories {
    mavenCentral()
    maven {
	url "https://mvnrepository.com/artifact/us.bpsm/edn-java"
    }
}

task getReleasePassword << {
    def password = ''

    if (rootProject.file('../private.properties').exists()) {
	Properties properties = new Properties();
	properties.load(rootProject.file
			('../private.properties').newDataInputStream())
	password = properties.getProperty('release.password')
    }

    if (!password?.trim()) {
	password = new String(System.console().readPassword("\nWhat's the secret password? "))
    }

    android.signingConfigs.release.storePassword = password
    android.signingConfigs.release.keyPassword = password
}

tasks.whenTaskAdded { theTask ->
    if (theTask.name.equals("packageRelease")) {
	theTask.dependsOn "getReleasePassword"
    }
}
